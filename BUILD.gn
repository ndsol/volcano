# Copyright (c) 2017 the Volcano Authors. Licensed under GPLv3.

declare_args() {
  is_skia_standalone = true
  use_spirv_cross_reflection = true
}

config("language_config") {
  if (!is_win) {
    cflags = [ "-Wno-missing-field-initializers" ]
  }
}

config("language_local_config") {
  include_dirs = [ get_path_info(".", "abspath" ) ]
}

static_library("language") {
  sources = [
    "src/language/choose.cpp",
    "src/language/debug.cpp",
    "src/language/device.cpp",
    "src/language/imageview.cpp",
    "src/language/log.cpp",
    "src/language/language.cpp",
    "src/language/queues.cpp",
    "src/language/requestqfams.cpp",
    "src/language/supported_queues.cpp",
    "src/language/swapchain.cpp",
    "src/language/VkEnum.cpp",
    "src/language/utf8dec.cpp",
    "src/language/utf8enc.cpp",
  ]

  public_deps = [ "//src/gn/vendor/vulkansamples" ]

  public_configs = [ ":language_local_config" ]
  all_dependent_configs = [ ":language_config" ]
  public = [
    "src/language/language.h",
  ]
}

static_library("command") {
  sources = [
    "src/command/command.cpp",
    "src/command/fence.cpp",
    "src/command/find_in_paths.cpp",
    "src/command/mmap.cpp",
    "src/command/pipeline.cpp",
    "src/command/pipeline_create.cpp",
    "src/command/render.cpp",
    "src/command/shader.cpp",
    "src/command/subpass.cpp",
  ]

  deps = [ ":language" ]
  public_deps = [ "//src/gn/vendor/vulkansamples" ]

  public_configs = [ ":language_local_config" ]
  public = [
    "src/command/command.h",
  ]
}

source_set("memory") {
  sources = [
    "src/memory/add_depth.cpp",
    "src/memory/buffer.cpp",
    "src/memory/descriptor.cpp",
    "src/memory/image.cpp",
    "src/memory/memory.cpp",
    "src/memory/layout.cpp",
    "src/memory/sampler.cpp",
  ]

  deps = [
    ":command",
    ":language",
    "//src/gn/vendor/vulkansamples:vk_format_utils",
  ]
  public_deps = [ "//src/gn/vendor/vulkansamples" ]

  public_configs = [ ":language_local_config" ]
  public = [
    "src/memory/memory.h",
  ]
}

config("science_config") {
  if (use_spirv_cross_reflection) {
    defines = [ "USE_SPIRV_CROSS_REFLECTION" ]
  }
}

source_set("science") {
  sources = [
    "src/science/present.cpp",
    "src/science/science.cpp",
  ]
  deps = [
    ":command",
    ":language",
    ":memory",
  ]
  if (use_spirv_cross_reflection) {
    sources += [ "src/science/reflect.cpp" ]
    deps += [ "//src/gn/vendor/spirv_cross" ]
  }
  public_deps = [ "//src/gn/vendor/vulkansamples" ]

  public_configs = [
    ":language_local_config",
    ":science_config",
  ]
  public = [
    "science.h",
  ]
}

group("volcano") {
  deps = [
    ":science",
  ]
}

import("//src/gn/vendor/androidExecutable.gni")
group("default") {
  if (!is_android && android_variants_dir == "") {
    testonly = true
    deps = [
      "test:basic_test",
    ]
  }
}
